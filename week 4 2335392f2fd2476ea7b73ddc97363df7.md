# week 4

Created: May 22, 2022 10:56 AM
Reviewed: No

# L04 Interrupts – Software

### Terminology:  difference between trap and interrupt

1. “Trap”
    - triggered by an instruction （instruction can’t be executed/ instruction triggers mode switch）
        - system calls (int 0x80 or syscall / sysenter)
        - Undefined result (e.g. division by 0)
        - OS must do something (e.g. page fault)
        - Invalid instruction (e.g. programming error)
    - Properties
        - Synchronous to program execution
        - often predictable, often reproducible
        - Restart or abort the operation that triggers the trap
        - Traps can’t be suppressed
2. “Interrupt”
    - Triggered by hardware, i.e., Hardware requires attention by software
    - Properties
        - Asynchronous to program execution
        - not predictable, not reproducible
        - Usually resume the interrupted activity
        - Interrupt can be supressed (cli)

### Basic Assumptions

1. The CPU automatically starts the handler routine.
    - CPU use IDT to dispatch interrupt to handler automatically
        - IDT located in memory
        - Each IDT entry contain a function pointer to handler
2. Interrupt handling takes place in supervisor mode.
    - In order to protect devices from untrusted applications, only OS may access devices, i.e., before interrupt handling, the CPU switches to the privileged
    supervisor mode.
    - Interrupts are the only mechanism to preempt noncooperative applications ( by enter the privilege mode in order to suspend the app thread and launch the interrupt handler routine)
3. The interrupted program can be resumed.
    - necessary state is automatically saved in supervisor stack
    - nested interrupt possible, requires a supervisor  stack, however CPU need mechanism to prevent stack overflow from unlimited nested stack
4. Machine instructions are atomic.
    - Why?
        - OS need a defined CPU state when handler routine starts in order to restore the state after interrupt handling is done
    - How?
        - Trivial for CPUs with classic von Neumann (fetch-decode-execute) cycle
        - Nontrivial for modern CPUs supporting Pipelining and Superscalar (may need to flush the pipeline and need to leave the pipeline in a defined states)
        
        ![Untitled](week%204%202335392f2fd2476ea7b73ddc97363df7/Untitled.png)
        
        - Although nontrivial, most CPUs implement precise interrupts:
            - All instructions preceding the instruction indicated by the saved program
            counter have been executed and have modified the process state correctly.“
            - All instructions following the instruction indicated by the saved program counter are unexecuted and have not modified the process state.“
            - If the interrupt is caused by an exception condition raised by an instruction (trap) in the program, the saved program counter points to the interrupted instruction. The interrupted instruction may or may not have been executed, depending on the definition of the architecture and the cause of the interrupt. Whichever is the case, the interrupted instruction has either completed, or has not started execution.“
5. Interrupt handling can be disabled/suppressed
    - Example:
        - Intel x86: globally with sti, cli
        - Interrupt Controller(PIC): each source individually, i.e., use pic to disable devices attached to certain interrupt line
    - Additionally: Automatic suppression by the CPU before starting the handler routine, why?,
        - Interrupts not predictable (theoretically arbitrarily frequent!)
        - Without this automatism, a stack overflow would be possible
    - The hardware suppresses has 2 options:
        - suppresses  across the board all interrupts (very restrictive)
        - suppresses  interrupts with lower or same priority (less restrictive)
        - suppresses interrupts that are currently being handled, example：
            - After keyboard interrupt happens, the CPU signal the pic to disable the keyboard interrupt. Then CPU enable the global interrupt by `sti`. After OS handled keyboard interrupt, it enable the keyboard interrupt by modify the pic control registers.
        

### Saving State

1. Motivation for saving state before interrupt handling
    
    Both the main program and the interrupt service subroutine use the CPU's internal registers and other resources. In order for the interrupt handler not to destroy the registers  contents of the main program, the register  contents at the interrupted point should first be pressed into the stack to protect it.
    
2. What resource should be saved
- In general, any state the interrupted program does not expect to asynchronously change,  must not be modified in an interrupt handler  must be saved, and restored afterwards
- 2 type of state saving
    - Total save
        - Handler routine saves all registers that were not automatically saved
        - Pro: saved state easily accessible (one coherent data structure)
        - Con: probably saves too much,  may need a lot of space
    - Partial save
        - Handler only saves registers that
            - are modified in the interrupt handler
            - are not saved/restored by other parts of the handler or the functions it calls directly or indirectly
            - Pro
                - only state that actually gets modified is saved/restored (save memory space)
                - possibly less instructions for save/restore necessary
            - Con:
                - need figure out which registers are touch by handler
                - saved state is “scattered” (not in one place, hard to access)
1. Transition to High-Level Language
    - The call instruction in wrapper save the volatile register
    
    ![Untitled](week%204%202335392f2fd2476ea7b73ddc97363df7/Untitled%201.png)
    
    When interrupt happens, the CPU automatically save the PC and status register into supervisor stack and jump to wrapper function. The wrapper function save the volatile registers on the stack and calls interrupt handler based on the IDT number (IDT number is passed to wrapper func as parameter). Note that the non-volatile registers are saved and restored by compiler automatically. After the handler finish handling the interrupt, CPU jump back to wrapper, in which the previous saved registers restored. Finally, the PC and status register stored by the last instruction `rte` in the wrapper.
    
2. Volatile and Non-Volatile Registers
    - Partitioning of CPU registers relevant for the (C/C++) compiler in the
    context of function calls
    - non-volatile (aka callee-saved) registers:
        - Compiler guarantees that the stored value is conserved across function calls
        - Callee (=called function) is responsible: If it uses the register, it saves/restores value.
    - volatile (aka caller-saved or scratch) registers
        - If the caller (=calling function) still needs the value after a function call, it must save/restore the register itself.
3. Restoring State
    - As its last duty, wrapper must restore saved register contents, and must not again modify them afterwards!
    - A special instruction (e.g. rte or iret) completes the restore procedure:
        - Reads automatically saved state from supervisor stack
        - Sets the saved CPU mode (user/supervisor), jumps to saved address

### Example: Interrupts on x86 with PIC

1. Hardware for Interrupt req
    - x86 CPUs up to and including i486 only have only one interrupt line (`INT`) + one `NMI` line
        - `INT` can be masked with IE bit in EFLAGS register using `cli` (disable interrupt handling)and `sti` (enable interrupt handling)
        - `NMI` cannot be masked in the CPU (“non-maskable interrupt”)
    - How to support interrupt from multiple devices?
        - Use External controller and let it puts IRQ number on memory bus, i.e, Use Programmable Interrupt Controller to multiplex the `INT`
2. Workflow of Hardware IRQ with PIC
    1. PIC send interrupt req to CPU
    2. CPU interrupt app and save app state, then send ack to PIC, which means the CPU is ready to handle the IRQ
    3. PIC put IRQ number on CPU Date bus
    4. CPU pick up the IRQ number and send ack to PIC
    5. CPU look at the IDT , find the proper handler, then call this handler
    6. After the Interrupt is handled by the handler,  handler return control to cpu
    7. CPU resume the execution of app again 
3. Interrupt Descriptor Table
    - Has max . 256 entries
    - The base address and size is stored in IDTR
    - Each entry in IDT can be:
        - Task gate (Hardware tasks)
        - Trap gate (Exception handler)
        - Interrupt gate (**Exception handler + cli:**  CPU disable the interrupt automatically while handling a interrupt)
    - Entry 0-31 are for traps (Exception that occurs synchronously to control flow, e.g, Page fault, Breakpoint ..)  (固定的)
    - Entries 32–255 for IRQs (configurable):  we can define
        - Software trap (system call) (INT <number>)
        - Hardware interrupt (CPU’s INT pin would go to HIGH if interrupt is coming, #number is placed on data bus by PIC)
    - Format of Descriptor in the IDT
        
        ![Untitled](week%204%202335392f2fd2476ea7b73ddc97363df7/Untitled%202.png)
        
4. State Saving
    1. When an interrupt occurs, the CPU automatically saves a part of its state on the stack
        1. Stack pointer (rsp)
        2. Return address (rip)
        3. For some exceptions (=“traps”): additionally an error code (8 bytes)
        4. Stack frame pointer(rbp)
        5. etc.
    2. Automatically saved state is restored by `iretq` instruction

1. How does OOSTUBE setup interrupt and handle incoming interrupt?
    - Set up IDT entries according to IDT descriptor format
        - At the compile time we don’t know the address of each wrapper function, So we store the relative address/offset to the wrapper_0 to IDT entries for each wrapper function
        
        ```wasm
        [SECTION .data]
        ;
        ; Interrupt descriptor table with 256 entries
        ; dw 2bytes  dd 4bytes 
        
        idt:
        %macro idt_entry 1  ; 1 represents the first argument 
        	dw  (wrapper_%1 - wrapper_0) & 0xffff ; offset 0 .. 15
        	dw  0x0000 | 0x8 * 2 ; selector points to 64-bit code segment selector (GDT)
        	dw  0x8e00 ; 8 -> interrupt is present, e -> 80386 32-bit interrupt gate
        	dw  ((wrapper_%1 - wrapper_0) & 0xffff0000) >> 16 ; offset 16 .. 31
        	dd  ((wrapper_%1 - wrapper_0) & 0xffffffff00000000) >> 32 ; offset 32..63
        	dd  0x00000000 ; reserved
        %endmacro
        
        ; Generate IDT entries
        %assign i 0  
        %rep 256
        idt_entry i
        %assign i i+1
        %endrep
        
        idt_descr:
        	dw  256*8 - 1    ; 256 entries define how many entries in IDT
        	dq idt    ; define IDT start address
        ```
        
    - Relocate the IDT entries and load the IDT to IDTR register
        - Until at the run time, we know the address of wrapper_0,  therefore we go through each entries in IDT to update the address for each wrapper function (address of wrapper_0 + the relative address/offset)
        
        ```cpp
        ;
        ; Relocating of IDT entries and setting IDTR
        ;
        
        setup_idt:
        	mov    rax, wrapper_0
        
        	; bits 0..15 -> ax, 16..31 -> bx, 32..64 -> edx
        	mov    rbx, rax
        	mov    rdx, rax
        	shr    rdx, 32
        	shr    rbx, 16
        
        	mov    r10, idt   ; pointer to the actual interrupt gate
        	mov    rcx, 255   ; counter
        .loop:   ;update the address for each wrapper function (address of wrapper_0 + the relative address/offset)
        	add    [r10+0], ax
        	adc    [r10+6], bx
        	adc    [r10+8], edx
        	add    r10, 16
        	dec    rcx
        	jge    .loop
        
        	lidt   [idt_descr]
        	ret
        ```
        
    - When the interrupt is coming, the wrapper function corresponding to the IRQ number will be called,  the wrapper function save the volatile register and calls guardian. Note wrapper uses `rdi` to pass the IRQ number to guardian
        
        ```cpp
        ; Before the wrapper get called ,
        ; CPU save the rsp, rflags, cs, ss, ip registers automatically
        ; template for header for each interrupt-handling routine
        %macro wrapper 1
        wrapper_%1:
        	push   rbp
        	mov    rbp, rsp
        	push   rax
        	mov    al, %1
        	jmp    wrapper_body
        %endmacro
        
        ; common handler body
        wrapper_body:
        	; GCC expects the direction flag to be 0
        	cld
        	; save volatile registers
        	push   rcx
        	push   rdx
        	push   rdi
        	push   rsi
        	push   r8
        	push   r9
        	push   r10
        	push   r11
        
        	; the generated wrapper only gives us 8 bits, mask the rest
        	and    rax, 0xff
        
        	; pass interrupt number as the first parameter
        	mov    rdi, rax
        	call   guardian
        
        	; restore volatile registers
        	pop    r11
        	pop    r10
        	pop    r9
        	pop    r8
        	pop    rsi
        	pop    rdi
        	pop    rdx
        	pop    rcx
        
        	; ... also those from the wrapper
        	pop    rax
        	pop    rbp
        
        	; done
        	iretq ; the instruction restore the rsp, rflags, cs, ss, ip registers
        
        ;
        ```
        

### State Modifications

1. Concept:
    - State Modifications are the main purpose of interrupt handling
        - Inform device driver about completed I/O operation
        - notify scheduler that time slice has run out
    - must be performed with care:
        - Interrupts can occur at any time
        - critical: Data/data structures shared between regular control flow
        and interrupt handling
2. Examples
    - System Time
        
        ![Untitled](week%204%202335392f2fd2476ea7b73ddc97363df7/Untitled%203.png)
        
        ![Untitled](week%204%202335392f2fd2476ea7b73ddc97363df7/Untitled%204.png)
        
        Here the problem is the contention between interrupt handler and OS function time() triggered by application. For 16 bit CPU, the read OP is not atomic. Assume the time() is executing, and move the lower part of the variable `gloabl_time` to variable `result` , at this point, the timer interrupt is triggered, the interrupt handler increments the `global_time` by 1 and resume the time function, the time function then continue mov the higher part of `global_time` variable to variable result. **This process results in inconsistency in variable `results`.**
        
    - Ring Buffer
        
        ![Untitled](week%204%202335392f2fd2476ea7b73ddc97363df7/Untitled%205.png)
        
        - occupied: how many elements are in the buffer
        - nextin: next position to write data into the buffer
        - nextout: next position to read data out of the buffer
        
        ![Untitled](week%204%202335392f2fd2476ea7b73ddc97363df7/Untitled%206.png)
        
        - Only produce can interrupt consumer, for example, keyboard interrupt trigger the producer to insert a key into the buffer
        - Problem after `cosume`() finished:
            - occupied is 0, but the buffer dose have 1 element
            - when occupied is 0, `nextin` and `nextout` should point to the same location
        - Here the inconsistency is caused by the copy `int elements = occupied`
        
        **Q: Why we can’t use semaphore to do synchronization here?**
        

### Hard” Synchronization (one possible solution for inconsistency issues in ring buffer)

![Untitled](week%204%202335392f2fd2476ea7b73ddc97363df7/Untitled%207.png)

- disable interrupt →avoid race condition
- problem
    - Hazard of losing interrupt requests
    - High and difficult to predict “interrupt latency”

![Untitled](week%204%202335392f2fd2476ea7b73ddc97363df7/Untitled%208.png)